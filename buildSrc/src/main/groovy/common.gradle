ext {
    javaVersion = JavaVersion.VERSION_17
    checkstyleVersion = '7.8.1'
    junitVersion = '5.8.1'
    groupId = 'printscript' // Define el groupId globalmente
    publishVersion = '2.2.0'

    configurePlugins = { project ->
        project.plugins.apply 'java'
        project.plugins.apply 'maven-publish'
        project.plugins.apply 'jacoco'
        project.plugins.apply 'checkstyle'
    }

    configureRepositories = { project ->
        project.repositories {
            mavenCentral()
        }
    }

    configureCheckstyle = { project ->
        project.checkstyle {
            toolVersion = checkstyleVersion
            configFile = project.file("${project.rootDir}/buildSrc/checkstyle.xml")
        }
        project.tasks.withType(Checkstyle) {
            source = 'src/main/java'
        }
    }

    configurePublishing = { project, artifactId -> // Configura groupId aquí
        project.group = groupId // Usa el groupId global
        project.version = publishVersion // Usa la versión global

        project.publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/ignamendez01/printscript")
                    credentials {
                        username = System.getenv('GITHUB_ACTOR') ?: 'defaultUser'
                        password = System.getenv('GITHUB_TOKEN') ?: 'defaultToken'
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    from project.components.java
                    artifactId = artifactId // Asigna el artifactId de cada módulo
                }
            }
        }
    }

    configureTesting = { project ->
        project.tasks.test {
            useJUnitPlatform()
            finalizedBy project.tasks.jacocoTestReport
            environment "NAME", "Ignacio"
            environment "NUMBER_TO_SUM", 25
            jacocoTestReport {
                reports {
                    xml.required = true
                    html.required = true
                }
            }
        }
    }
}

