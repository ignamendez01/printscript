plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'checkstyle'  // Agregar Checkstyle
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":ast")
    testImplementation project(":lexer")
    testImplementation project(":parser")
    testImplementation project(":token")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

checkstyle {
    toolVersion '10.12.0'
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source ='src/main/java'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Para que siempre se genere el reporte tras ejecutar los tests
    environment "NAME", "Ignacio"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ignamendez01/printscript")
            credentials {
                username = System.getenv('GITHUB_ACTOR') ?: 'defaultUser'
                password = System.getenv('GITHUB_TOKEN') ?: 'defaultToken'
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'printscript'
            artifactId = 'interpreter'
            version = '2.1.5'
        }
    }
}

